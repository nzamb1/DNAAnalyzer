apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

/*
 * Add to the project "local.properties" file following lines:
 * facebook_app_id=YOUR_ID_HERE
 * fb_login_protocol_scheme=YOUR_PROTOCOL_SCHEME_HERE
 */
ext.props = new Properties()
ext.props.load(project.rootProject.file('local.properties').newDataInputStream())

def getProp(String name) {
    def result = ext.props.getProperty(name)
    if (result == null) {
        throw new GradleException("${name} must be defined in local.properties")
    }
    return result
}

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.dnaanalyzer"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 6
        versionName "6.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        resValue 'string', 'facebook_app_id', getProp('facebook_app_id')
        resValue 'string', 'fb_login_protocol_scheme', getProp('fb_login_protocol_scheme')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'junit', module: 'junit'
    }
    implementation 'com.google.firebase:firebase-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.facebook.android:facebook-login:[5,6)'

    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
